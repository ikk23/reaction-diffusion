// 1D simulation with periodic boundaries, sexual reproduction, non-overlapping generations, male reproductive success based on fitness, and female fecundity based on fitness and local density
initialize() {
	initializeSLiMModelType("nonWF");
	initializeSLiMOptions(dimensionality="x", periodicity="x");
	initializeSex("A");
	
	// Parameters that can be changed at the command line
	defineCfgParam("a", 0.06);
	defineCfgParam("sigma",0.01);
	defineCfgParam("k", 0.2);
	defineCfgParam("u_hat", 0.4);
	// save the boundaries for the drive release
	defineConstant("drive_x_min", 0.5 - (a/2));
	defineConstant("drive_x_max", 0.5 + (a/2));
	defineConstant("DRIVE_RELEASE_GENERATION",10); // when to release the drive
	defineConstant("alpha", 1 - 2*u_hat);
	defineConstant("beta", a/sigma);
	catn("-----------------------------------");
	catn("a = " + a + " sigma = " + sigma + " so beta = " + beta);
	defineConstant("m", 0.001); // mating radius
	catn("-----------------------------------");
	print(paste("Frequency threshold of:", u_hat, " and alpha of: " + alpha));
	defineConstant("POPSIZE", 30000);
	defineConstant("CHROM_LENGTH", 1);
	d_d_fitness = 	1 + (2*alpha*k);
	d_wt_fitness = 1 + ((alpha-1)*k);
	wt_wt_fitness = 1;
	fitnesses = c(d_d_fitness, d_wt_fitness, wt_wt_fitness);
	defineConstant("drive_homozygote_fitness", fitnesses[0]);
	defineConstant("drive_heterozygote_fitness", fitnesses[1]);
	defineConstant("wt_homozygote_fitness", fitnesses[2]);
	print(paste("d/d fitness:",drive_homozygote_fitness, "d/wt fitness:", drive_heterozygote_fitness, "wt/wt fitness:", wt_homozygote_fitness));
	catn("-----------------------------------");
	
	defineConstant("GROWTH_AT_ZERO_DENSITY", 2);
	defineConstant("EXPONENT_OF_COMPETITION_FACTOR", 1);
	defineConstant("EXPECTED_ADULT_COMPETITION", POPSIZE*(2*m));
	catn("-----------------------------------");
	catn("EXPECTED_ADULT_COMPETITION when m is " + m + " = " + EXPECTED_ADULT_COMPETITION);
	
	// Plot the number of drive alleles, drive homozygotes, and drive heterozygotes
	// between [0.5-MIDPOINT_WINDOW_WIDTH] and [0.5+MIDPOINT_WINDOW_WIDTH]
	defineConstant("LIVEPLOT", T);
	defineConstant("MIDPOINT_WINDOW_WIDTH", 0.001);
	defineConstant("window_lower", 0.5 - MIDPOINT_WINDOW_WIDTH);
	defineConstant("window_upper", 0.5 + MIDPOINT_WINDOW_WIDTH);
	if (LIVEPLOT){
		if (fileExists("/usr/bin/Rscript"))
			defineConstant("RSCRIPT", "/usr/bin/Rscript");
		else if (fileExists("/usr/local/bin/Rscript"))
			defineConstant("RSCRIPT", "/usr/local/bin/Rscript");
		else
			stop("Couldn't find Rscript.");
	}
	
	// CHECKS	
	defineConstant("VIABILITY_SELECTION", T);
	defineConstant("EXPECTED_NUM_OFFSPRING_MULTIPLIER", 2.0); // this*p is used in the Poisson distribution if VIABILITY_SELECTION=True
	defineConstant("PANMICTIC_CHECK", F); // remove all spatial processes
	// don't use if VIABILITY_SELECTION is true
	defineConstant("GLOBAL_DENSITY_CONTROL", F); // should also be true if PANMICTIC_CHECK is true. If F, local density control is used.	
	defineConstant("NO_DRIVE", F); // don't release drive at all -- for wt N testing	
	defineConstant("MATING_DEACTIVATION", F);
	defineConstant("RELEASE_HETEROZYGOTES", F);
	defineConstant("WT_MIDDLE_PERCENTAGE", 0.8);
	defineConstant("lower_homz", 0.5 - ((WT_MIDDLE_PERCENTAGE/2)*a));
	defineConstant("upper_homz", 0.5 + ((WT_MIDDLE_PERCENTAGE/2)*a));
	defineConstant("RELEASE_DRIVE_EVERYWHERE", F); // if F, only release between -a/2 and a/2
	defineConstant("b", 0.45); // intro frequency if RELEASE_DRIVE_EVERYWHERE is true. If not, this is 1 inside the release square.
	
	// Chromosome of length 1. The mutation can either be drive or wt.
	initializeMutationRate(0.0);
	initializeRecombinationRate(0.0);
	wt = initializeMutationType("m1", 0.5, "f", 0.0);
	dr = initializeMutationType("m2", 0.5, "f", 0.0);
	variations = c(wt, dr);
	
	variations.mutationStackPolicy = "l";
	variations.mutationStackGroup = 1;
	
	initializeGenomicElementType("g1", variations, c(1, 1));
	initializeGenomicElement(g1, 0, CHROM_LENGTH-1);
	
	// Interaction for mating and local competition
	initializeInteractionType(1, "x", reciprocal=T, maxDistance=m);

}

//// HELPER FUNC FOR CONSTANTS THAT MAY ASLO BE CONFIGURED VIA COMMAND LINE.
function (void) defineCfgParam(string$ name, lifs value) {
	if (!exists(name))
		defineConstant(name, value);
}


1 {
	sim.addSubpop("p1", POPSIZE);
	
	all = p1.individuals;
	all.x = runif(POPSIZE, min = 0, max = 1);
	
	all.genomes.addNewDrawnMutation(m1, seqLen(CHROM_LENGTH));
	all.tagF = wt_homozygote_fitness;
	
	if (LIVEPLOT){
	
		// PATH TO PLOT FOR GENOTYPE COUNTS
		defineConstant("pngPath", writeTempFile("plot_", ".png", ""));
		// If we're running in SLiMgui, open a plot window
		if (exists("slimgui")) slimgui.openDocument(pngPath);		
		
		// PATH TO PLOT FOR OVERALL DRIVE RATE
		defineConstant("pngPathForDriveRate",writeTempFile("plot2_", ".png", ""));
		if (exists("slimgui")) slimgui.openDocument(pngPathForDriveRate);	
		
		// within window 
		sim.setValue("d", NULL); // count of drive alleles
		sim.setValue("dd", NULL); // count of dd homozygotes
		sim.setValue("dwt",NULL); // count of dwt heterozygotes
		
		// over entire range
		sim.setValue("drive_rate", NULL); // overall drive rate
	}

}

reproduction(NULL, "F") {
	
	
	if (!PANMICTIC_CHECK){
		all_neighbors = i1.nearestNeighbors(individual, subpop.individualCount);
		neighbors = all_neighbors[all_neighbors.sex == "M"]; // male neighbors
		num_neighbors = length(neighbors);
		
		// females in empty areas cannot mate
		if (num_neighbors == 0){
			catn("empty area - can't mate"); // should be rare
			return;
		}
		
		if (!VIABILITY_SELECTION){
			mating_weights = neighbors.tagF;
			mate_id = sample(x = seqLen(num_neighbors), size = 1, weights = mating_weights);
		} else {
			mate_id = sample(x = seqLen(num_neighbors), size = 1);
		}
		selected_mate = neighbors[mate_id];
	
	
	} else {
		// male can be anywhere if this is panmictic
		all = p1.individuals;
		males = all[all.sex=="M"];
		mating_weights = males.tagF;
		mate_id = sample(x = seqLen(length(males)), size = 1, weights = mating_weights);
		selected_mate = males[mate_id];
	}
	
	
	// local density control
	if (!GLOBAL_DENSITY_CONTROL & !PANMICTIC_CHECK){
		adult_density_force = i1.totalOfNeighborStrengths(individual);
		competition_ratio = adult_density_force / EXPECTED_ADULT_COMPETITION;
		actual_competition_factor = GROWTH_AT_ZERO_DENSITY/ ((GROWTH_AT_ZERO_DENSITY-1) * competition_ratio + 1);
		actual_competition_factor_exponentiated = actual_competition_factor^EXPONENT_OF_COMPETITION_FACTOR;
		if (!VIABILITY_SELECTION){
			female_fitness = individual.tagF;
			p = female_fitness * actual_competition_factor_exponentiated;
		} else {
			p = actual_competition_factor_exponentiated; // fitness doesn't affect fecundity, but the # offspring is limited by clustering
		}
	} else {
		female_fitness = individual.tagF;
		p = female_fitness;
	}
	
	if (!VIABILITY_SELECTION){
		expected_value = 2*p;
	} else {
		expected_value = p*EXPECTED_NUM_OFFSPRING_MULTIPLIER;
	}
	num_offspring = rpois(1,expected_value);
	
	for (i in seqLen(num_offspring)) {
		offspring = subpop.addCrossed(individual, selected_mate);
	}

}

1: modifyChild() {
	
	// at the drive release generation, convert all offspring whose parents were within the release square to d/d
	gen = sim.generation;
	if (gen == DRIVE_RELEASE_GENERATION){
		
		if (!RELEASE_HETEROZYGOTES & !RELEASE_DRIVE_EVERYWHERE & !PANMICTIC_CHECK){
			if (parent1.x >= drive_x_min & parent1.x <= drive_x_max){
				child.genomes.addNewDrawnMutation(m2, seqLen(CHROM_LENGTH));
			}
		} else if (RELEASE_HETEROZYGOTES) {
			// heterozygous from drive_x_min to lower_homz
			// homozygous from lower_homz to upper_homz
			// heterozygous from upper_homz to drive_x_max
			if (parent1.x >= drive_x_min & parent1.x < lower_homz){
				child.genome1.addNewDrawnMutation(m1, seqLen(CHROM_LENGTH));
				child.genome1.addNewDrawnMutation(m2, seqLen(CHROM_LENGTH));
			} else if (parent1.x >= lower_homz & parent1.x < upper_homz){
				child.genomes.addNewDrawnMutation(m2, seqLen(CHROM_LENGTH));
			} else if (parent1.x >= upper_homz & parent1.x < drive_x_max){
				child.genome1.addNewDrawnMutation(m1, seqLen(CHROM_LENGTH));
				child.genome1.addNewDrawnMutation(m2, seqLen(CHROM_LENGTH));
			}
		} else if (PANMICTIC_CHECK | RELEASE_DRIVE_EVERYWHERE){
			if (runif(1) < b){
				child.genomes.addNewDrawnMutation(m2, seqLen(CHROM_LENGTH));
			}
		}
	}
	
	wildtype = child.countOfMutationsOfType(m1) == 2*CHROM_LENGTH;
	drive_hom = child.countOfMutationsOfType(m2) == 2*CHROM_LENGTH;
	drive_het = (child.countOfMutationsOfType(m1) == CHROM_LENGTH) & (child.countOfMutationsOfType(m2) == CHROM_LENGTH);
	
	
	// genotype determines fitness and color
	if (wildtype){
		child.tagF = wt_homozygote_fitness;
	} else if (drive_hom) {
		child.tagF = drive_homozygote_fitness;
	} else if (drive_het) {
		child.tagF = drive_heterozygote_fitness;
	} else {
		// shouldn't ever occur
		print("Unknown genotype -- there are " + child.countOfMutationsOfType(m1) + " wt mutations and " + child.countOfMutationsOfType(m2) + " drives");
	}
	
	if (!PANMICTIC_CHECK){
		// draw a displacement distance from the exponential distribution with an average dispersal distance of sigma
		distance = rexp(1,mu = sigma);
		
		// if this draw is 1, go right. If it's 0, go left.
		right = rbinom(1, 1, 0.5);
		if (right){
			position = parent1.x + distance;
		} else {
			position = parent1.x - distance;
		}
	
	} else {
		position = parent1.x; // no movement
	}
	child.setSpatialPosition(p1.pointPeriodic(position));
	
	return T;
}


// Kill off adults
// why was this in a late() event in our gene drive models?
// check this
early() {
	
	all = p1.individuals;
	all[all.age > 0].fitnessScaling = 0.0;
	
	
	if (GLOBAL_DENSITY_CONTROL){
		juveniles = all[all.age==0];
		juveniles.fitnessScaling = POPSIZE/length(juveniles);
	}
	
	if (VIABILITY_SELECTION){
		juveniles = all[all.age==0];
		
		// probability of juvenial survival is now dependent on relative fitness value
		juveniles.fitnessScaling = juveniles.tagF/drive_homozygote_fitness; // genotype-based fitness values play a role in survival probability
	}


}


// non-WF specific
// output the generation, population size, and frequency of the drive allele
// evaluate spatial distances for the next round of reproduction
// end the simulation if the drive is lost or fixed
late() {
	
	gen = sim.generation;
	
	if (!NO_DRIVE & gen >= DRIVE_RELEASE_GENERATION){
		N = p1.individualCount;
		all = p1.individuals;
		num_drive_alleles = sum(all.genomes.countOfMutationsOfType(m2)==CHROM_LENGTH);
		rate_dr = num_drive_alleles/(2*N);
		
		
		
		// Separate inds by genotype
		drive_homozygotes = all[all.countOfMutationsOfType(m2) == 2*CHROM_LENGTH];
		drive_heterozygotes = all[all.countOfMutationsOfType(m2)==CHROM_LENGTH];
		wild_types = all[all.countOfMutationsOfType(m1)==2*CHROM_LENGTH];
		homoz_freq = length(drive_homozygotes)/N; // freq(DD)
		het_freq = length(drive_heterozygotes)/N; // freq(Dd)
		
		// count the number of drive alleles
		num_dr = sum(all.genomes.countOfMutationsOfType(m2)==CHROM_LENGTH);
		n_d_wt = length(drive_heterozygotes);
		n_d_d = length(drive_homozygotes);
		
		
				
		// Color
		all.color = "blue";
		drive_homozygotes.color = "red";
		drive_heterozygotes.color = "orange";
		
		
		if (!LIVEPLOT){
		catn("GEN:: " + gen + " N: " + N + " rate_dr: " + rate_dr + " num_drive: " + num_dr + " num_d_d: " + n_d_d + " num_d_wt: " + n_d_wt);
		}
		//catn("Generation: " + gen + " N: " + N + " rate_dr: " + rate_dr);
		
		// live plotting
		if (LIVEPLOT) {
		
			// get the number of drive alleles inside the window
			drive_homz_inside_window = drive_homozygotes[drive_homozygotes.x >= window_lower  & drive_homozygotes.x <= window_upper];
			drive_hetz_inside_window = drive_heterozygotes[drive_heterozygotes.x >= window_lower & drive_heterozygotes.x <= window_upper];
			
			n_d_d = length(drive_homz_inside_window);
			n_d_wt = length(drive_hetz_inside_window);
			num_dr = (2*n_d_d) + n_d_wt;
		
		
			catn("GEN:: " + gen + " N: " + N + " overall drive rate: " + rate_dr + " d_in_window: " + num_dr + " d_d_in_window: " + n_d_d + " d_wt_in_window: " + n_d_wt);
		
			sim.setValue("drive_rate", c(sim.getValue("drive_rate"), rate_dr));
			sim.setValue("d", c(sim.getValue("d"), num_dr));
			sim.setValue("dd", c(sim.getValue("dd"), n_d_d));
			sim.setValue("dwt", c(sim.getValue("dwt"), n_d_wt));		
			
			d_alleles = sim.getValue("d");
			d_d_count = sim.getValue("dd");
			d_wt_count = sim.getValue("dwt");
			dr_rate_overall = sim.getValue("drive_rate");
			
			// PLOT OF GENOTYPE AND ALLELE COUNTS WITHIN THE WINDOW
			rstr = paste('{',
				'library(tidyverse)',
				'info = tibble(generation = c(' + paste(seqLen(gen-10+1), sep = ", ") + '), drive_homozygotes = c(' + paste(d_d_count, sep=", ") + '), drive_heterozygotes = c(' + paste(d_wt_count, sep = ", ") + '), drive_count = c(' + paste(d_alleles, sep=", ") + '))',
				'p = ggplot(info, aes(x = generation)) + geom_line(aes(y=drive_homozygotes), color = "red") + geom_line(aes(y=drive_heterozygotes), color = "orange") + geom_line(aes(y=drive_count), color = "purple") + ylim(0,' + POPSIZE*MIDPOINT_WINDOW_WIDTH*2*2 +') + xlim(0, 100) + xlab("Generation") + ylab("Count") + ggtitle("Homozygote count (red), heterozygote count (orange),\ndrive allele count (purple). Window = [' + window_lower + ',' + window_upper + ']")',
				'png("' + pngPath + '")',
				'print(p)',
				'dev.off()',
				'}',	
			sep='\n');
			scriptPath = writeTempFile("plot_", ".R", rstr);
			system(RSCRIPT, args=scriptPath);
			
			
			// PLOT OF OVERALL DRIVE RATE
			rstr2 = paste('{',
				'library(tidyverse)',
				'info = tibble(generation = c(' + paste(seqLen(gen-10+1), sep = ", ") + '), drive_rate = c(' + paste(dr_rate_overall, sep=", ") + '))',
				'p = ggplot(info, aes(x = generation, y = drive_rate)) + geom_line(color = "black") + ylim(0, 1) + xlim(0, 100) + xlab("Generation") + ylab("Frequency") + ggtitle("Drive allele frequency over entire range")',
				'png("' + pngPathForDriveRate + '")',
				'print(p)',
				'dev.off()',
				'}',	
			sep='\n');
			scriptPath2 = writeTempFile("plot2_", ".R", rstr2);
			system(RSCRIPT, args=scriptPath2);
			
		
			deleteFile(scriptPath);
			deleteFile(scriptPath2);
			
		
		}
		
		
		
		if (rate_dr == 0){
			sim.simulationFinished();
		} // else if (rate_dr == 1){
		//sim.simulationFinished();
		//}
	
	} else {
		N = p1.individualCount;
		gen = sim.generation;
		p1.individuals.color = "blue";
		catn("Generation: " + gen + " N: " + N);
	}
	
	if (!PANMICTIC_CHECK){
		i1.evaluate();
	}

}


//Stop the simulation after 100 generations
110 late() { sim.simulationFinished(); }
